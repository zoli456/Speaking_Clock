; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
#include "CodeDependencies.iss"
#define MyAppName "Beszélő óra"
#define MyAppVersion "1.60"
#define MyAppPublisher "Zoli456 Software"
#define MyAppURL "https://zsoftware.ct.ws/speaking_clock.html"
#define MyAppExeName "Speaking_Clock.exe"
#define VCREDIST_X86_URL "https://aka.ms/vs/17/release/vc_redist.x86.exe"
#define VCREDIST_X64_URL "https://aka.ms/vs/17/release/vc_redist.x64.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{2DE96183-A1E5-408C-BBCC-CF513F65AF0C}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
; "ArchitecturesAllowed=x64compatible" specifies that Setup cannot run
; on anything but x64 and Windows 11 on Arm.
ArchitecturesAllowed=x64compatible
; "ArchitecturesInstallIn64BitMode=x64compatible" requests that the
; install be done in "64-bit mode" on x64 or Windows 11 on Arm,
; meaning it should use the native 64-bit Program Files directory and
; the 64-bit view of the registry.
ArchitecturesInstallIn64BitMode=x64compatible
DisableProgramGroupPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
PrivilegesRequired=admin
OutputBaseFilename=Speaking_clock_setup
SetupIconFile=G:\Programok\Speaking_Clock\Speaking_Clock\download-icon-clock-131964752299044081_128.ico
Compression=lzma
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "hungarian"; MessagesFile: "compiler:Languages\Hungarian.isl"

[Components]
Name: "main"; Description: "Alkalmazás"; Types: full compact custom; Flags: fixed
Name: "vcredist"; Description: "Microsoft Visual C++ 2015–2022 futtatókörnyezet"; Types: full custom

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "G:\Programok\Speaking_Clock\Speaking_Clock\bin\x64\Release\net9.0-windows10.0.19041.0\protected\*"; DestDir: "{app}\"; Flags: ignoreversion recursesubdirs createallsubdirs

; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent runascurrentuser;


[Code]
function InitializeSetup: Boolean;
begin
Dependency_AddDotNet90Desktop;
  Result := True;
end;

var
  DownloadPage: TDownloadWizardPage;

//
// ---------------- WIZARD INIT ----------------
//
procedure InitializeWizard;
var
  ResultCode: Integer;
begin
  if not (IsWin64 and (GetWindowsVersion >= $0A000000)) then
  begin
    MsgBox('Ennek az alkalmazásnak Windows 10 (64-bit) vagy újabbra rendszerre van szüksége. A telepítés megszakadt.', mbError, MB_OK);
    Abort;
  end;
  DownloadPage :=
    CreateDownloadPage(
      'Fájlok letöltés',
      'Kérem várjon amíg a szükséges fájlok letöltése folyamatban van.',
      nil
    );
end;

//
// ---------------- DOWNLOAD + INSTALL ----------------
//
procedure DownloadAndInstallVCRedist(const Url, FileName: String);
var
  ResultCode: Integer;
begin
  DownloadPage.Clear;
  DownloadPage.Add(Url, FileName, '');
  DownloadPage.Show;

  try
    DownloadPage.Download;
  except
    MsgBox(
      'Sikertelen letöltés:'#13#10 + Url,
      mbError,
      MB_OK
    );
    Abort;
  end;

  DownloadPage.Hide;

  if not Exec(
    ExpandConstant('{tmp}\' + FileName),
    '/passive /norestart',
    '',
    SW_SHOW,
    ewWaitUntilTerminated,
    ResultCode
  ) then
  begin
    MsgBox(
      'Visual C++ futtatókörnyezet telepítése sikertelen.',
      mbError,
      MB_OK
    );
    Abort;
  end;
end;

//
// ---------------- INSTALL PHASE ----------------
//
function PrepareToInstall(var NeedsRestart: Boolean): String;
begin
  if WizardIsComponentSelected('vcredist') then
  begin
    DownloadAndInstallVCRedist(
      '{#VCREDIST_X86_URL}',
      'vc_redist.x86.exe'
    );

    DownloadAndInstallVCRedist(
      '{#VCREDIST_X64_URL}',
      'vc_redist.x64.exe'
    );
  end;

  Result := '';
end;